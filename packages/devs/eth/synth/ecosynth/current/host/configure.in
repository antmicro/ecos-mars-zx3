dnl Process this file with autoconf to produce a configure script.
dnl ====================================================================
dnl
dnl     configure.in
dnl
dnl     configure script for eCos synthetic target ethernet
dnl	host-side support
dnl
dnl ====================================================================
dnl ####ECOSHOSTGPLCOPYRIGHTBEGIN####                                       
dnl -------------------------------------------                             
dnl This file is part of the eCos host tools.                               
dnl Copyright (C) 2002 Free Software Foundation, Inc.                       
dnl
dnl This program is free software; you can redistribute it and/or modify    
dnl it under the terms of the GNU General Public License as published by    
dnl the Free Software Foundation; either version 2 or (at your option) any  
dnl later version.                                                          
dnl
dnl This program is distributed in the hope that it will be useful, but     
dnl WITHOUT ANY WARRANTY; without even the implied warranty of              
dnl MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU       
dnl General Public License for more details.                                
dnl
dnl You should have received a copy of the GNU General Public License       
dnl along with this program; if not, write to the                           
dnl Free Software Foundation, Inc., 51 Franklin Street,                     
dnl Fifth Floor, Boston, MA  02110-1301, USA.                               
dnl -------------------------------------------                             
dnl ####ECOSHOSTGPLCOPYRIGHTEND####                                         
dnl ====================================================================
dnl#####DESCRIPTIONBEGIN####
dnl
dnl Author(s):	bartv
dnl Contact(s):	bartv
dnl Date:	2002/08/07
dnl Version:	0.01
dnl
dnl####DESCRIPTIONEND####
dnl ====================================================================


AC_INIT(rawether.c)

dnl Pick up the support files from the top-level acsupport directory.
AC_CONFIG_AUX_DIR(../../../../../../../acsupport)

ECOS_CHECK_BUILD_ne_SRC
AC_CANONICAL_HOST
AM_INIT_AUTOMAKE(eCos_synthetic_target_ethernet,0.1,0)
AM_MAINTAINER_MODE
AC_PROG_CC
AC_PROG_CXX
AC_OBJEXT
AC_EXEEXT
ECOS_PROG_MSVC
ECOS_PROG_STANDARD_COMPILER_FLAGS
ECOS_PACKAGE_DIRS

dnl The current version of the synthetic target is implemented only for
dnl x86 Linux platforms, so a test is appropriate here. However
dnl it is not a good idea for the configure script to report an error:
dnl that would prevent any top-level configury working for other
dnl platforms. Instead an automake conditional is used to suppress adding
dnl targets to the build.
case "${host}" in
    i[[34567]]86-*-linux-gnu* ) SUPPORTED="yes";;
    x86_64-*-linux-gnu* ) SUPPORTED="yes";;
    * ) SUPPORTED="no"
esac
if test "${SUPPORTED}" = "no" ; then
    AC_MSG_WARN([Synthetic target ethernet support is only available on x86 Linux hosts])
fi   

if test "${SUPPORTED}" = "yes" ; then
  dnl Old kernels may not have tun/tap support. rawether can
  dnl still operate via a spare ethernet interface.
  AC_CHECK_HEADERS("linux/if_tun.h",TAP_SUPPORTED="yes",TAP_SUPPORTED="no")
  if test "${TAP_SUPPORTED}" = "no" ; then
    AC_MSG_WARN([No <linux/if_tun.h> header, ethertap support disabled.])
  fi
fi

if test "${SUPPORTED}" = "no" ; then
  AC_MSG_WARN([The synthetic ethernet support cannot be built on this platform.])
fi

AM_CONDITIONAL(SUPPORTED, test "${SUPPORTED}" = "yes")

dnl There is no real need for a config.h file at this time, since the code
dnl is specific to x86 Linux. This may change in future.
dnl AM_CONFIG_HEADER(config.h:config.h.in)

AC_OUTPUT(Makefile:Makefile.in)
